<inicio> ::= <instrucciones>

<instrucciones> ::= <instrucciones> <instruccion>
                  | <instruccion>

<instruccion> ::= <impresion> "FINCADENA"
                | <Correr>
                | <declaracion> "FINCADENA"
                | <asignacion> "FINCADENA"
                | <insIncremento>
                | <insDecremento>
                | <SentMetodo>
                | <sentIf>
                | <sentFor>
                | <sentWhile>
                | <sentMatch>
                | <sentReturn>
                | <sentBreak>
                | <sentContinue>
                | <Llamada> "FINCADENA"
                | "error" "FINCADENA"
                | "error" "RKEY"

<impresion> ::= "IMPRIMIR" "PAR1" <expresion> "PAR2"

<Correr> ::= "RUN" <ID> "PAR1" <paramLlamada> "PAR2" "FINCADENA"
           | "RUN" <ID> "PAR1" "PAR2" "FINCADENA"

<Llamada> ::= <ID> "PAR1" <paramLlamada> "PAR2"
            | <ID> "PAR1" "PAR2"

<sentReturn> ::= "RETURN" "FINCADENA"
               | "RETURN" <expresion> "FINCADENA"

<sentBreak> ::= "BREAK" "FINCADENA"

<sentContinue> ::= "CONTINUE" "FINCADENA"

<declaracion> ::= "LET" <ID> ":" <tipos> "=" <expresion>
                | "LET" <ID> ":" <tipos>
                | "LET" <ID> ":" "LIST" "<" <tipos> ">"
                | "LET" <ID> ":" <tipos> "[]" "=" "[" <lista_valoresVUD> "]"
                | "LET" <ID> ":" <tipos> "[][]" "=" "[" <lista_valoresVBD> "]"
                | "CONST" <ID> ":" <tipos> "=" <expresion>
                | "CONST" <ID> ":" <tipos>
                | "CONST" <ID> ":" <tipos> "[]" "=" "[" <lista_valoresVUD> "]"
                | "CONST" <ID> ":" <tipos> "[][]" "=" "[" <lista_valoresVBD> "]"

<asignacion> ::= <ID> "=" <expresion>
               | <ID> "." "APPEND" "PAR1" <expresion> "PAR2"
               | <ID> "." "REVERSE" "PAR1" "PAR2"
               | <ID> "[" <expresion> "]" "=" <expresion>
               | <ID> "." "SET" "PAR1" <expresion> "," <expresion> "PAR2"
               | <ID> "[" <expresion> "]" "[" <expresion> "]" "=" <expresion>

<lista_valoresVUD> ::= <lista_valoresVUD> "," <expresion>
                     | <expresion>

<lista_valoresVBD> ::= <lista_valoresVBD> "," "[" <lista_valoresVUD> "]"
                     | "[" <lista_valoresVUD> "]"

<SentMetodo> ::= <tipos> <ID> "PAR1" "PAR2" "LKEY" <instrucciones> "RKEY"
               | <tipos> <ID> "PAR1" <params> "PAR2" "LKEY" <instrucciones> "RKEY"

<params> ::= <params> "," <parametro>
           | <parametro>

<parametro> ::= <ID> ":" <tipos> "=" <expresion>
              | <ID> ":" <tipos>

<paramLlamada> ::= <paramLlamada> "," <ID> "=" <expresion>
                 | <ID> "=" <expresion>

<sentIf> ::= "IF" "PAR1" <expresion> "PAR2" "LKEY" <instrucciones> "RKEY"
           | "IF" "PAR1" <expresion> "PAR2" "LKEY" <instrucciones> "RKEY" "ELSE" "LKEY" <instrucciones> "RKEY"
           | "IF" "PAR1" <expresion> "PAR2" "LKEY" <instrucciones> "RKEY" "ELSE" <sentIf>

<sentFor> ::= "FOR" "PAR1" <asignacion> "FINCADENA" <expresion> "FINCADENA" <actFor> "PAR2" "LKEY" <instrucciones> "RKEY"

<actFor> ::= <asignacion>
           | <insIncremento>
           | <insDecremento>

<sentWhile> ::= "WHILE" "PAR1" <expresion> "PAR2" "LKEY" <instrucciones> "RKEY"
              | "DO" "LKEY" <instrucciones> "RKEY" "WHILE" "PAR1" <expresion> "PAR2" "FINCADENA"

<sentMatch> ::= "MATCH" <expresion> "LKEY" <listaCasos> "DEFAULT" "TO" "LKEY" <instrucciones> "RKEY" "RKEY"

<listaCasos> ::= <caso>
               | <listaCasos> <caso>

<caso> ::= <expresion> "TO" "LKEY" <instrucciones> "RKEY"

<insIncremento> ::= <ID> "INCREMENTO" "FINCADENA"
                  | <ID> "INCREMENTO"

<insDecremento> ::= <ID> "DECREMENTO" "FINCADENA"
                  | <ID> "DECREMENTO"

<expresion> ::= "-" <expresion>
              | <expresion> "+" <expresion>
              | <expresion> "-" <expresion>
              | <expresion> "*" <expresion>
              | <expresion> "/" <expresion>
              | <expresion> "^" <expresion>
              | <expresion> "%" <expresion>
              | <expresion> "==" <expresion>
              | <expresion> "!=" <expresion>
              | <expresion> ">" <expresion>
              | <expresion> ">=" <expresion>
              | <expresion> "<" <expresion>
              | <expresion> "<=" <expresion>
              | <expresion> "AND" <expresion>
              | <expresion> "OR" <expresion>
              | "NOT" <expresion>
              | "CAST" "PAR1" <expresion> "AS" <tipos> "PAR2"
              | "ROUND" "PAR1" <expresion> "PAR2"
              | "LENGTH" "PAR1" <expresion> "PAR2"
              | "TOSTRING" "PAR1" <expresion> "PAR2"
              | "ENTERO"
              | "DECIMAL"
              | "CARACTER"
              | "CADENA"
              | "BOOLEANO"
              | "PAR1" <expresion> "PAR2"
              | <ID>
              | <ID> "." "GET" "PAR1" <expresion> "PAR2"
              | <ID> "." "REMOVE" "PAR1" <expresion> "PAR2"
              | <ID> "." "POP" "PAR1" "PAR2"
              | <ID> "[" <expresion> "]"
              | <ID> "[" <expresion> "]" "[" <expresion> "]"
              | <Llamada>

<tipos> ::= "INT"
          | "DOUBLE"
          | "CHAR"
          | "STRING"
          | "BOOL"
          | "VOID"
